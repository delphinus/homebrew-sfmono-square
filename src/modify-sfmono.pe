#!/usr/local/bin/fontforge -script

Print("Generate modified SF Mono")

input = $argv[1]
output = $argv[2]

Print("Open " + input)
Open(input)
SelectWorthOutputting()
UnlinkReference()

# Scale down all glyphs
Print("Scale down all glyphs")
SelectWorthOutputting()
Scale(80.8847, 0, 0)
SetWidth(1024)

# copy lacked glyphs from regular fonts to italic ones
regular_font = ""
if (input == "SFMono-RegularItalic.otf")
    regular_font = "SFMono-Regular.otf"
elseif (input == "SFMono-BoldItalic.otf")
    regular_font = "SFMono-Bold.otf"
endif
if (regular_font != "")
    MergeFonts(regular_font)
endif

# move glyphs to use these codepoints for Material fonts
Select(0xf6d5); Copy(); Select(0xe800); Paste()
Select(0xf6d6); Copy(); Select(0xe801); Paste()
Select(0xf6d7); Copy(); Select(0xe802); Paste()
Select(0xf6d8); Copy(); Select(0xe803); Paste()
Select(0xf8ff); Copy(); Select(0xe804); Paste()
Select(0xfb01); Copy(); Select(0xe805); Paste()
Select(0xfb02); Copy(); Select(0xe806); Paste()

RoundToInt(); RemoveOverlap(); RoundToInt()

Print("Clear instructions")
SelectWorthOutputting()
ClearInstrs()

Print("Save " + output)
Save(output)
Close()

Quit()

# vim:se ft=fontforge_script:
